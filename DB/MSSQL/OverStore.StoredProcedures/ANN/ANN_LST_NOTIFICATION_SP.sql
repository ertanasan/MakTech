CREATE PROCEDURE ANN_LST_NOTIFICATION_SP @IncludeSystemType VARCHAR(1) = 'N' AS  
BEGIN  

    IF OBJECT_ID('tempdb.dbo.#USERS', 'U') IS NOT NULL DROP TABLE #USERS;

	SELECT U.USERID INTO #USERS
	  FROM dbo.STR_GETUSERSTORES_FN() A
	  JOIN SEC_USER U ON A.BRANCH = U.BRANCH

	DECLARE @bs INT = 0;
	SELECT @bs = COUNT(*) FROM STR_REGIONMANAGERS WHERE USERID = dbo.sys_getcurrentuser_fn();

    DECLARE @Organization INT;
    SELECT @Organization = dbo.SYS_GETCURRENTORGANIZATION_FN();
    IF dbo.SYS_ISSYSTEMORGANIZATION_FN() = 1
    BEGIN
      SET @Organization = null;
    END;

    -- Calculate storeCount  
    WITH UsersCount AS (  
    SELECT N.NOTIFICATIONID, COUNT(NU.[USER]) USER_CNT  
         , SUM(CASE WHEN PN.STATUS_CD = 4 THEN 1 ELSE 0 END) READ_CNT  
      FROM ANN_NOTIFICATION N (NOLOCK)  
      LEFT JOIN ANN_NOTIFICATIONUSER NU (NOLOCK) ON N.NOTIFICATIONID = NU.[NOTIFICATION]  
      LEFT JOIN BPM_PROCESSINSTANCE PN (NOLOCK) ON NU.PROCESSINSTANCE_LNO = PN.PROCESSINSTANCEID   
	 WHERE (@bs = 0 OR NU.[USER] IN (SELECT USERID FROM #USERS))
     GROUP BY N.NOTIFICATIONID)  
  
    SELECT N.NOTIFICATIONID,  
           N.EVENT,  
           N.ORGANIZATION,
           N.DELETED_FL,
           N.CREATE_DT,
           N.UPDATE_DT,
           N.CREATEUSER,
           N.UPDATEUSER,
           N.CREATECHANNEL,
           N.CREATEBRANCH,
           N.CREATESCREEN,
           N.NOTIFICATION_TXT,
           N.NOTIFICATIONTYPE,
           N.NOTIFICATIONSTATUS,
		   UC.USER_CNT,
		   UC.READ_CNT,
           N.FOLDER
      FROM ANN_NOTIFICATION N (NOLOCK)
	  JOIN UsersCount UC ON N.NOTIFICATIONID = UC.NOTIFICATIONID
     WHERE (@Organization IS NULL OR N.ORGANIZATION = @Organization)
       AND DELETED_FL = 'N'
       AND ((@IncludeSystemType = 'N' AND NOTIFICATIONTYPE != 1) OR @IncludeSystemType = 'Y')
     ORDER BY N.NOTIFICATIONID DESC;
  
END;
