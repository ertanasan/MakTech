CREATE PROCEDURE WHS_UPD_STARTCONTROL_SP @GatheringPalletId BIGINT, @AllowReControl VARCHAR(1), @ErrorCode INT OUT AS
BEGIN
	DECLARE @paletStatus INT, @orderStatus INT, @gatheringId BIGINT, @PaletNo INT, @ControlUser INT;
    DECLARE @currentUser INT = dbo.SYS_GETCURRENTUSER_FN();
	SET @ErrorCode = 0;

	BEGIN TRANSACTION TRX;
    
	SELECT @paletStatus = GP.GATHERINGPALLETSTATUS, @orderStatus = SO.STATUS, @gatheringId = G.GATHERINGID, @PaletNo = GP.PALLET_NO, @ControlUser = GP.CONTROLUSER
	  FROM WHS_GATHERING G
	  JOIN WHS_GATHERINGPALLET GP WITH (UPDLOCK, ROWLOCK, READPAST) ON G.GATHERINGID = GP.GATHERING
	  JOIN WHS_STOREORDER SO ON G.STOREORDER = SO.STOREORDERID
	  JOIN STR_STORE ST ON SO.STORE = ST.STOREID
	  JOIN WHS_GATHERINGTYPE GT ON G.GATHERINGTYPE = GT.GATHERINGTYPEID
	 WHERE GP.GATHERINGPALLETID = @GatheringPalletId;
	
	IF ((@paletStatus = 1 AND @orderStatus = 3) -- beklemede ve sevk edilmemiş durumda
		OR
		(@orderStatus = 3 AND @AllowReControl = 'Y')) -- tekrar control edilecekse. 
	BEGIN
		INSERT INTO WHS_GATHERINGPALLETLOG (GATHERINGPALLETID, LOG_DT, LOGUSER, LOGOPERATION_CD, LOGCHANNEL, LOGBRANCH, LOGSCREEN, GATHERING, PALLET_NO, CONTROLUSER, CONTROLSTART_TM, CONTROLEND_TM, GATHERINGPALLETSTATUS)
		SELECT GATHERINGPALLETID, GETDATE(), dbo.SYS_GETCURRENTUSER_FN(), 'UPD', dbo.SYS_GETCURRENTCHANNEL_FN(), dbo.SYS_GETCURRENTBRANCH_FN(), dbo.SYS_GETCURRENTSCREEN_FN(), 
			   GATHERING, PALLET_NO, CONTROLUSER, CONTROLSTART_TM, CONTROLEND_TM, GATHERINGPALLETSTATUS
		  FROM WHS_GATHERINGPALLET G (NOLOCK)
         WHERE G.GATHERINGPALLETID = @GatheringPalletId;
		UPDATE WHS_GATHERINGPALLET 
		   SET CONTROLUSER = dbo.SYS_GETCURRENTUSER_FN()
			 , CONTROLSTART_TM = GETDATE()
			 , CONTROLEND_TM = NULL
			 , GATHERINGPALLETSTATUS = 2 -- kontrol ediliyor
		 WHERE GATHERINGPALLETID = @GatheringPalletId
	END ELSE IF (@paletStatus = 2 AND @ControlUser = @currentUser AND @orderStatus = 3) -- kendi başlattığı işe devam ediyor
	BEGIN
	  SET @ErrorCode = 0; -- kendi başlattığı işe devam ediyor
	END	ELSE IF (@orderStatus != 3)
	BEGIN
	  SET @ErrorCode = 1; -- sevk edilmiş
	END ELSE IF @paletStatus = 2 
	BEGIN
	  SET @ErrorCode = 2; -- şu an toplanıyor
	END ELSE IF @paletStatus = 9
	BEGIN
	  SET @ErrorCode = 3; -- zaten kontrol edilmiş
	END 
	COMMIT TRANSACTION TRX;  
END