CREATE PROCEDURE RCL_UPD_EXPENSERECOVER_SP AS
BEGIN

	IF OBJECT_ID('tempdb.dbo.#EXPENSEUPDATE', 'U') IS NOT NULL  DROP TABLE #EXPENSEUPDATE;

	WITH EXPENSES AS (
	SELECT E.STORE, CAST(E.CREATE_DT AS DATE) EXPENSE_DT, SUM(EXPENSE_AMT) EXPENSE_AMT
	  FROM RCL_EXPENSE E
	  JOIN SEC_USER U ON E.CREATEUSER = U.USERID
	  JOIN STR_STORE ST ON U.BRANCH = ST.BRANCH AND ST.ACTIVE_FL = 'Y'
	 WHERE E.DELETED_FL = 'N'
	   AND ST.DELETED_FL = 'N'
	 GROUP BY E.STORE, CAST(E.CREATE_DT AS DATE) ),
	PAYOFFS AS (
	SELECT E.STORE, CAST(E.PAYOFF_DT AS DATE) PAYOFF_DT, SUM(EXPENSE_AMT) EXPENSERETURN_AMT
	  FROM RCL_EXPENSE E
	  JOIN SEC_USER U ON E.CREATEUSER = U.USERID
	  JOIN STR_STORE ST ON U.BRANCH = ST.BRANCH AND ST.ACTIVE_FL = 'Y'
	 WHERE E.DELETED_FL = 'N'
	   AND E.OPEN_FL = 'N'
	   AND ST.DELETED_FL = 'N'
	 GROUP BY E.STORE, CAST(E.PAYOFF_DT AS DATE)) 
	SELECT RS.STORERECID, ST.STOREID, ST.STORE_NM, CAST(D.DATE_DT AS DATE) DATE_DT, ISNULL(E.EXPENSE_AMT,0) EXPENSE_AMT, RS.EXPENSE_AMT RCLEXPENSE_AMT, 
		   ISNULL(P.EXPENSERETURN_AMT, 0) EXPENSERETURN_AMT, RS.EXPENSERETURN_AMT RCLEXPENSERETURN_AMT 
	  INTO #EXPENSEUPDATE
	  FROM STR_STORE ST  
	  JOIN RPT_DATE D ON D.DATE_DT BETWEEN ST.OPENING_DT AND ISNULL(ST.CLOSING_DT, CONVERT(DATE, '20991231', 112))  
	  JOIN RCL_STORE RS ON ST.STOREID = RS.STORE AND D.DATE_DT = RS.RECONCILIATION_DT
	  LEFT JOIN EXPENSES E ON ST.STOREID = E.STORE AND D.DATE_DT = E.EXPENSE_DT
	  LEFT JOIN PAYOFFS P ON ST.STOREID = P.STORE AND D.DATE_DT = P.PAYOFF_DT
	 WHERE D.DATE_DT BETWEEN '2019-04-29' and CAST(GETDATE()-1 AS DATE)
	   AND ST.DELETED_FL = 'N'  
	   AND ST.ACTIVE_FL = 'Y'
	   -- AND ST.STOREID != 152
	   -- AND ST.STOREID = 107
	   AND (ISNULL(E.EXPENSE_AMT, 0) != ISNULL(RS.EXPENSE_AMT, 0) OR
			ISNULL(P.EXPENSERETURN_AMT, 0) != ISNULL(RS.EXPENSERETURN_AMT, 0))
	 ORDER BY 3 DESC

	UPDATE R SET R.EXPENSE_AMT = E.EXPENSE_AMT, R.EXPENSERETURN_AMT = E.EXPENSERETURN_AMT
	-- SELECT *
	  FROM #EXPENSEUPDATE E
	  JOIN RCL_STORE R ON E.STORERECID = R.STORERECID

	DECLARE rcl_expense CURSOR FOR   
	  SELECT DISTINCT STOREID FROM #EXPENSEUPDATE

	DECLARE @storeId INT

	SET NOCOUNT ON  
	OPEN rcl_expense    
	FETCH NEXT FROM rcl_expense INTO @storeId

	WHILE @@FETCH_STATUS = 0    
	BEGIN   
	  EXEC RCL_UPD_RECALCULATE_SP @storeId

	  FETCH NEXT FROM rcl_expense INTO @storeId
	END

	CLOSE rcl_expense    
	DEALLOCATE rcl_expense   
	SET NOCOUNT OFF  

END