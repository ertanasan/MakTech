CREATE PROCEDURE [dbo].[WHS_LST_WDGATHERINGTREND_SP] 
	@GatheringType INT, -- Heavy (1) or Light (2)
	@TrendDataType INT, -- Amount (1) or Package (2) or Weight (3)
	@DayCount INT,
	@GatheringUserId INT = -1 -- -1 for all gatheringUsers
WITH RECOMPILE AS
BEGIN  
  
	DECLARE @StartDate DATE
	SET @StartDate = CAST(GETDATE() - @DayCount AS DATE)

	IF OBJECT_ID('tempdb.dbo.#RESULTS', 'U') IS NOT NULL DROP TABLE #RESULTS;
	SELECT U.USERID, U.USERFULL_NM, CAST(GD.GATHERING_TM AS date) GATHER_DT
		 , SUM(GD.GATHERED_QTY) PACKAGE_QTY
		 , SUM(GD.GATHERED_QTY * SOD.ORDERUNIT_QTY * P.WEIGHT_AMT / 1000.0) WEIGHT_QTY
		 , SUM(GD.GATHERED_QTY * SOD.ORDERUNIT_QTY * SP.PRICE_AMT) PRICE_AMT
		 , SUM(CASE WHEN CONTROL_QTY IS NOT NULL AND CONTROL_QTY != GATHERED_QTY THEN 1 ELSE 0 END) CONTROLERR_CNT
		 , SUM(CASE WHEN CONTROL_QTY IS NOT NULL AND CONTROL_QTY != GATHERED_QTY THEN ABS(CONTROL_QTY - GATHERED_QTY)  ELSE 0 END) CONTROLDIFF_CNT
	  INTO #RESULTS
	  FROM WHS_GATHERINGDETAIL GD
	  JOIN WHS_GATHERING G ON GD.GATHERING = G.GATHERINGID
	  JOIN SEC_USER U ON G.GATHERINGUSER = U.USERID
	  JOIN WHS_STOREORDERDETAIL SOD ON GD.STOREORDERDETAIL = SOD.STOREORDERDETAILID
	  JOIN PRD_PRODUCT P ON SOD.PRODUCT = P.PRODUCTID
	  LEFT JOIN PRC_SALEPRICE_VW SP ON P.PRODUCTID = SP.PRODUCT
	 WHERE GD.GATHERING_TM >= @StartDate
	   AND (@GatheringUserId = -1 OR U.USERID = @GatheringUserId)
	   AND ((@GatheringType  = 1 AND P.LOADORDER_TXT LIKE 'A%')
			OR 
			(@GatheringType  = 2 AND P.LOADORDER_TXT NOT LIKE 'A%'))
	 GROUP BY U.USERID, U.USERFULL_NM, CAST(GD.GATHERING_TM AS date)

	SELECT U.USERID GATHERUSERID, U.USERFULL_NM GATHERUSER_NM
		 , D.DATE_DT GATHER_DT
		 , ISNULL(
		   CASE @TrendDataType 
		   WHEN 1 THEN PRICE_AMT
		   WHEN 2 THEN PACKAGE_QTY
		   WHEN 3 THEN WEIGHT_QTY
		   END, 0) GATHER_QTY
		 , ISNULL(CONTROLERR_CNT,0) CONTROLERR_CNT
		 , ISNULL(CONTROLDIFF_CNT, 0) CONTROLDIFF_CNT
		 , ISNULL(PACKAGE_QTY, 0) GATHERPACKAGE_QTY
	  FROM RPT_DATE D 
	  JOIN (SELECT DISTINCT USERID, USERFULL_NM FROM #RESULTS) U ON 1=1
	  LEFT JOIN #RESULTS R ON D.DATE_DT = R.GATHER_DT AND U.USERID = R.USERID
	 WHERE DATE_DT BETWEEN @StartDate AND CAST(GETDATE() AS DATE)
	 ORDER BY 1, 3

END