CREATE PROCEDURE [dbo].[WHS_SEL_WDGATHERPERFORMANCESUMMARY_SP] 
	@GatheringType INT -- 1:Ağır 2:Hafif
AS
BEGIN
	DECLARE @startDate DATE = CONVERT(DATE, '20200210', 112)
	DECLARE @today DATE  
	SELECT @today = MAX(DATE_DT)  
	  FROM RPT_DATE  
	 WHERE DATE_DT <= CAST(GETDATE() AS DATE)  
	   AND DAYOFWEEK_NO != 7  

	DECLARE @yesterday DATE  
	SELECT @yesterday = MAX(DATE_DT)  
	  FROM RPT_DATE  
	 WHERE DATE_DT < CAST(GETDATE() AS DATE)  
	   AND DAYOFWEEK_NO != 7  

	DECLARE @lastweek DATE = DATEADD(DAY, -7, CAST(@today AS DATETIME))

	IF CAST(GETDATE() - 30 AS DATE) > @startDate SET @startDate = CAST(GETDATE() - 30 AS DATE)
	DECLARE @pastWeightAvg NUMERIC(22,6), @todayWeight NUMERIC(22,6), @lastWeekWeight NUMERIC(22,6), @yesterdayWeight NUMERIC(22,6)
	DECLARE @pastDurationAvg NUMERIC(22,6), @todayDuration NUMERIC(22,6), @pastErrorCountAvg NUMERIC(22,6), @todayErrorCount NUMERIC(22,6)

	-- today weight
	SELECT @todayWeight = SUM(GATHERINGWEIGHT_QTY), @todayErrorCount = SUM(CONTROLERR_CNT)
	  FROM WHS_GATHERSUMMARY_VW
	 WHERE GATHERINGTYPE = @GatheringType
	   AND GATHER_DT = @today

	-- yesterday weight
	SELECT @yesterdayWeight = SUM(GATHERINGWEIGHT_QTY)
	  FROM WHS_GATHERSUMMARY_VW
	 WHERE GATHERINGTYPE = @GatheringType
	   AND GATHER_DT = @yesterday

	-- lastweek weight
	SELECT @lastWeekWeight = SUM(GD.GATHERED_QTY * SOD.ORDERUNIT_QTY * P.WEIGHT_AMT / 1000.0)
	  FROM WHS_GATHERING G
	  JOIN WHS_GATHERINGDETAIL GD ON G.GATHERINGID = GD.GATHERING
	  JOIN WHS_STOREORDERDETAIL SOD ON GD.STOREORDERDETAIL = SOD.STOREORDERDETAILID
	  JOIN PRD_PRODUCT P ON SOD.PRODUCT = P.PRODUCTID
	 WHERE GATHERINGTYPE = @GatheringType
	   AND G.DELETED_FL = 'N'
	   AND GD.DELETED_FL = 'N'
	   AND CAST(GATHERING_TM AS DATE) = @lastweek
	   AND CAST(GATHERING_TM AS TIME) < CAST(GETDATE() AS TIME)

	DECLARE @control INT
	SELECT @control = COUNT(*) FROM WHS_DASHBOARDHISTORY WHERE DATE_DT = @today  AND GATHERINGTYPE = @GatheringType AND VALUE_DSC = 'PASTWEIGHTAVG'
	IF @control > 0 
	BEGIN
		SELECT @pastWeightAvg = VALUE_AMT FROM WHS_DASHBOARDHISTORY WHERE DATE_DT = @today AND GATHERINGTYPE = @GatheringType AND VALUE_DSC = 'PASTWEIGHTAVG'
	END
	ELSE
	BEGIN
		SELECT @pastWeightAvg = SUM(GATHERINGWEIGHT_QTY) / COUNT(DISTINCT GATHER_DT)
		  FROM WHS_GATHERSUMMARY_SYN
		 WHERE GATHERINGTYPE = @GatheringType
		   AND GATHER_DT >= @startDate 
		   AND GATHER_DT < @today
		INSERT INTO WHS_DASHBOARDHISTORY VALUES (@today, @GatheringType, 'PASTWEIGHTAVG', @pastWeightAvg)
	END

	SELECT @pastDurationAvg = SUM(CASE WHEN GATHER_DT < @today THEN DURATION ELSE 0 END) / SUM(CASE WHEN GATHER_DT < @today THEN TOTALWEIGHT ELSE 0 END)
		 , @todayDuration = SUM(CASE WHEN GATHER_DT = @today THEN DURATION ELSE 0 END) / SUM(CASE WHEN GATHER_DT = @today THEN TOTALWEIGHT ELSE 0 END)
	  FROM WHS_GATHERINGDURATION_VW 
	 WHERE GATHERINGTYPE = @GatheringType
	   AND GATHER_DT >= @startDate 

	SELECT @pastErrorCountAvg = SUM(CONTROLERR_CNT) / COUNT(DISTINCT GATHER_DT)
	  FROM WHS_GATHERSUMMARY_SYN
	 WHERE GATHERINGTYPE = @GatheringType
	   AND GATHER_DT >= @startDate 

	SELECT @todayWeight TODAY_QTY, @lastWeekWeight THEDAYLASTWEEKUNTILNOW_QTY, @yesterdayWeight YESTERDAY_QTY, @pastWeightAvg DAILYAVG_QTY
		 , @todayDuration TODAYSECONDSPERKG, @pastDurationAvg AVGSECONDSPERKG, @todayErrorCount TODAYERROR_CNT, @pastErrorCountAvg DAILYAVGERROR_CNT
END