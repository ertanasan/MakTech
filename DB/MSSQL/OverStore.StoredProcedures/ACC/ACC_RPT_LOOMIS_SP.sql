CREATE PROCEDURE ACC_RPT_LOOMIS_SP @StartDate DATE, @EndDate DATE, @StoreId INT = -1 AS
BEGIN

	IF OBJECT_ID('tempdb.dbo.#RCL', 'U') IS NOT NULL DROP TABLE #RCL;
	IF OBJECT_ID('tempdb.dbo.#ZET', 'U') IS NOT NULL DROP TABLE #ZET;
	IF OBJECT_ID('tempdb.dbo.#SALE', 'U') IS NOT NULL DROP TABLE #SALE;
	IF OBJECT_ID('tempdb.dbo.#LOOMIS', 'U') IS NOT NULL DROP TABLE #LOOMIS;

	SELECT RECONCILIATION_DT, STORE, SUM(CARD_AMT) CARD_AMT, SUM(BANK_AMT) BANK_AMT
	  INTO #RCL
	  FROM RCL_STORE
	 WHERE RECONCILIATION_DT BETWEEN @StartDate AND @EndDate
	   AND DELETED_FL = 'N'
	 GROUP BY RECONCILIATION_DT, STORE


	SELECT TRANSACTION_DT, STORE, SUM(RECEIPTTOTAL_AMT + SLPTOTAL_AMT - REFUND_AMT) ZVALUE
	  INTO #ZET
	  FROM SLS_SALEZET
	 WHERE TRANSACTION_DT BETWEEN @StartDate AND @EndDate
	   AND DELETED_FL = 'N'
	 GROUP BY TRANSACTION_DT, STORE

	SELECT TRANSACTION_DT, STORE, SUM(TOTAL_AMT) SALEVALUE
	  INTO #SALE
	  FROM SLS_SALE
	 WHERE TRANSACTION_DT BETWEEN @StartDate AND @EndDate
	   AND CANCELLED_FL = 'N'
	   AND DELETED_FL = 'N'
	 GROUP BY TRANSACTION_DT, STORE

	SELECT TRANSACTION_DT, STOREID STORE, SUM(ACTUAL_AMT) LOOMIS, SUM(TRANSACTION_AMT) LOOMISDEPOSIT, MIN(STATEMENT_DT) DEPOSIT_DT
	  INTO #LOOMIS
	  FROM (
	SELECT SALE_DT TRANSACTION_DT, ST.STOREID, ST.STORE_NM, SEAL_TXT, ACTUAL_AMT
		 , COUNT(*) OVER (PARTITION BY ST.STOREID, ST.STORE_NM, SEAL_TXT) COUNTA
		 , ISNULL(BS.TRANSACTION_AMT, BS2.TRANSACTION_AMT) TRANSACTION_AMT
		 , ISNULL(BS.DATE_DT, BS2.DATE_DT) STATEMENT_DT
		 , ISNULL(BS.DESCRIPTION_DSC, BS2.DESCRIPTION_DSC) DESCRIPTION_DSC
	  FROM ACC_LOOMIS L
	  JOIN STR_STORE ST ON L.STORE = ST.STOREID
	  LEFT JOIN ACC_BANKSTATEMENT BS ON L.SEAL_TXT = BS.INFO3_DSC
	  LEFT JOIN ACC_BANKSTATEMENT BS2 ON ACTUAL_AMT != ISNULL(BS.TRANSACTION_AMT,0) AND L.SEAL_TXT != BS2.INFO3_DSC AND L.STORE = BS2.INFO2_DSC AND L.ACTUAL_AMT = BS2.TRANSACTION_AMT
	 WHERE SALE_DT BETWEEN @StartDate AND @EndDate) A
	 WHERE COUNTA = 1
	 GROUP BY TRANSACTION_DT, STOREID

	SELECT R.RECONCILIATION_DT DATE_DT, R.STORE, ST.STORE_NM, R.BANK_AMT, 
		   COALESCE(Z.ZVALUE, S.SALEVALUE) - ISNULL(R.CARD_AMT,0) ZMIN_AMT, ISNULL(L.LOOMIS,0) LOOMIS,
		   ISNULL(L.LOOMISDEPOSIT,0) LOOMISDEPOSIT, DEPOSIT_DT
	  FROM #RCL R
	  JOIN STR_STORE ST ON R.STORE = ST.STOREID
	  LEFT JOIN #ZET Z ON R.RECONCILIATION_DT = Z.TRANSACTION_DT AND R.STORE = Z.STORE
	  LEFT JOIN #SALE S ON R.RECONCILIATION_DT = S.TRANSACTION_DT AND R.STORE = S.STORE
	  LEFT JOIN #LOOMIS L ON R.RECONCILIATION_DT = L.TRANSACTION_DT AND R.STORE = L.STORE
	 WHERE (@StoreId = -1 OR ST.STOREID = @StoreId)
	 ORDER BY 1, 2

END