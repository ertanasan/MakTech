CREATE PROCEDURE [dbo].[INV_LST_STOCKDASHBOARDTREND_SP] @CategoryName VARCHAR(100), @ProductName VARCHAR(100), @StoreName VARCHAR(500) AS
BEGIN
	
	DECLARE @Date DATE
	SELECT @Date = MAX(DATE_DT) FROM INV_STOCKDASHBOARD

	DECLARE @CategoryId INT = -1
	DECLARE @ProductId INT = -1
	DECLARE @StoreId INT = -1

	IF @CategoryName != 'HEPSİ' SELECT @CategoryId = CATEGORYID FROM PRD_CATEGORY WHERE CATEGORY_NM = @CategoryName
	IF @ProductName != 'HEPSİ' SELECT @ProductId = PRODUCTID FROM PRD_PRODUCT WHERE NAME_NM = @ProductName
	IF @StoreName != 'HEPSİ' SELECT @StoreId = STOREID FROM STR_STORE WHERE STORE_NM = @StoreName

	SELECT STORE_NM, SUM(STOCK_AMT) STORESTOCK_AMT, SUM(STOCK_QTY) STORESTOCK_QTY, SUM(SALE_AMT) STORESALEAVG_AMT
	  FROM INV_STOCKDASHBOARD SD
	  JOIN STR_STORE ST ON SD.STORE = ST.STOREID
	  JOIN PRD_PRODUCT P ON SD.PRODUCT = P.PRODUCTID
	  LEFT JOIN PRD_SUBGROUP SG ON P.SUBGROUP = SG.SUBGROUPID
	  LEFT JOIN PRD_CATEGORY C ON SG.CATEGORY = C.CATEGORYID
	 WHERE P.SUPERGROUP1 != 9
	   AND STORE != 1001
	   AND SD.DATE_DT = @Date
	   AND (@CategoryId = -1 OR C.CATEGORYID = @CategoryId)
	   AND (@ProductId = -1 OR SD.PRODUCT = @ProductId)
	   AND (@StoreId = -1 OR SD.STORE = @StoreId)
	 GROUP BY STORE_NM

	SELECT DATE_DT "Days", 
		   SUM(CASE WHEN STORE != 1001 THEN STOCK_AMT ELSE 0 END) "StoreStock",
		   SUM(CASE WHEN STORE = 1001 THEN STOCK_AMT ELSE 0 END) "WarehouseStock"
	  FROM INV_STOCKDASHBOARD SD
	  JOIN PRD_PRODUCT P ON SD.PRODUCT = P.PRODUCTID
	  LEFT JOIN PRD_SUBGROUP SG ON P.SUBGROUP = SG.SUBGROUPID
	  LEFT JOIN PRD_CATEGORY C ON SG.CATEGORY = C.CATEGORYID
	 WHERE P.SUPERGROUP1 != 9
	   AND (@CategoryId = -1 OR C.CATEGORYID = @CategoryId)
	   AND (@ProductId = -1 OR SD.PRODUCT = @ProductId)
	   AND (@StoreId = -1 OR SD.STORE = @StoreId)
	 GROUP BY DATE_DT
	 ORDER BY 1
END