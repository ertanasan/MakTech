CREATE PROCEDURE WHS_INS_ADDPRODUCTTOCONTROL_SP @GatheringId BIGINT, @PalletNo INT, @ProductId INT AS 
BEGIN
	-- önceden kayıt atılmışsaa tekrar atmamak için kontrol
	DECLARE @existsControl INT 
	SELECT @existsControl = COUNT(*)
	  FROM WHS_GATHERINGDETAIL GD
	  JOIN WHS_STOREORDERDETAIL SOD ON GD.STOREORDERDETAIL = SOD.STOREORDERDETAILID
	 WHERE GATHERING = @GatheringId
	   AND GD.PALLET_NO = @PalletNo
	   AND SOD.PRODUCT = @ProductId

	IF @existsControl > 0 RETURN;

	-- siparişde ürün varsa id bilgisini al.
	DECLARE @StoreOrderDetailID BIGINT = NULL
	SELECT @StoreOrderDetailID = STOREORDERDETAILID
	  FROM WHS_GATHERING G
	  JOIN WHS_STOREORDERDETAIL SOD ON G.STOREORDER = SOD.STOREORDER
	 WHERE SOD.PRODUCT = @ProductId
	   AND G.GATHERINGID = @GatheringId

	-- siparişde ürün varsa event güncelle.
	IF @StoreOrderDetailID IS NOT NULL 
	BEGIN
		UPDATE WHS_STOREORDERDETAIL 
		   SET EVENT = 1059 
		 WHERE STOREORDERDETAILID = @StoreOrderDetailID
	END

	-- siparişde ürün yoksa insert et. 
	ELSE --@StoreOrderDetailID IS NULL 
	BEGIN
		INSERT INTO WHS_STOREORDERDETAIL
		(EVENT, ORGANIZATION, DELETED_FL, CREATE_DT, CREATEUSER, CREATECHANNEL, CREATEBRANCH, CREATESCREEN, 
		 PRODUCT, ORDER_QTY, REVISED_QTY, SHIPPED_QTY, ORDERUNIT_QTY, STOREORDER, INTAKE_QTY)
		SELECT 1059, SO.ORGANIZATION, 'N', GETDATE(), dbo.SYS_GETCURRENTUSER_FN(), dbo.SYS_GETCURRENTCHANNEL_FN(),
			   dbo.SYS_GETCURRENTBRANCH_FN(), dbo.SYS_GETCURRENTSCREEN_FN(), @ProductId, 0, 0, 0, PACKAGE_QTY, SO.STOREORDERID, 0
		  FROM WHS_GATHERING G
		  JOIN WHS_STOREORDER SO ON G.STOREORDER = SO.STOREORDERID
		  JOIN WHS_PRODUCTSHIPMENTUNIT SU ON SU.PRODUCT = @ProductId AND SHIPMENTTYPE = 1
		 WHERE G.GATHERINGID = @GatheringId;

		SELECT @StoreOrderDetailID = SCOPE_IDENTITY();

	END

	-- kontrol kaydı ekle.
	INSERT INTO WHS_GATHERINGDETAIL
	(EVENT, ORGANIZATION, DELETED_FL, CREATE_DT, CREATEUSER, CREATECHANNEL, CREATEBRANCH, CREATESCREEN, 
	 STOREORDERDETAIL, GATHERING_TM, GATHERING, GATHERED_QTY, PALLET_NO)
	SELECT EVENT, ORGANIZATION, 'N', GETDATE(), dbo.SYS_GETCURRENTUSER_FN(), dbo.SYS_GETCURRENTCHANNEL_FN(),
		   dbo.SYS_GETCURRENTBRANCH_FN(), dbo.SYS_GETCURRENTSCREEN_FN(), @StoreOrderDetailID, GETDATE(), @GatheringId, 0, @PalletNo
	  FROM WHS_GATHERING
	 WHERE GATHERINGID = @GatheringId
END