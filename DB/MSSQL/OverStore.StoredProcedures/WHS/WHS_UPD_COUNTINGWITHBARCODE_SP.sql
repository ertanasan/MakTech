CREATE PROCEDURE WHS_UPD_COUNTINGWITHBARCODE_SP @ScheduleID INT, @Zone INT, @OpCode INT, @EanCode VARCHAR(20), @ManualWeight NUMERIC(12,6), @FinalValue NUMERIC(12,6) OUT AS
BEGIN
  
  DECLARE @Barcode VARCHAR(20);
  SET @Barcode = @EanCode;
  IF (LEFT(@EanCode, 3) = '290') SET @Barcode = LEFT(@EanCode,7);

  DECLARE @Weight NUMERIC(12,6) 
  SET @Weight = @ManualWeight;
  IF (@ManualWeight = -1) 
  BEGIN
    SET @Weight = 1
    IF (LEFT(@EanCode, 3) = '290') SET @Weight = CAST(LEFT(RIGHT(@EanCode,6),5) AS INT)/1000.0;
  END
  
  DECLARE @ProductId INT, @StoreId INT, @CountingDate DATE

  SELECT @StoreId = STORE FROM WHS_STOCKTAKINGSCHEDULE WHERE STOCKTAKINGSCHEDULEID = @ScheduleID
  
  SET @CountingDate = CAST(GETDATE()-(6/24.0) AS DATE)
  
  -- PRINT @Barcode
  SELECT @ProductId = PRODUCT FROM PRD_BARCODE 
   WHERE BARCODE_TXT = @Barcode
     AND PRODUCT NOT IN (SELECT PRODUCT FROM PRD_PROPERTY WHERE PROPERTYTYPE = 6 AND VALUE_TXT = '1')
  
  IF @@ROWCOUNT > 0 
  BEGIN
    UPDATE WHS_STOCKTAKING 
	   SET COUNTINGVALUE_AMT = CASE WHEN @OpCode = 1 THEN @Weight ELSE ISNULL(COUNTINGVALUE_AMT,0) + @Weight END
	     , @FinalValue = CASE WHEN @OpCode = 1 THEN @Weight ELSE ISNULL(COUNTINGVALUE_AMT,0) + @Weight END
		 , UPDATEUSER = dbo.SYS_GETCURRENTUSER_FN()
		 , UPDATE_DT = GETDATE()
	 WHERE STOCKTAKINGSCHEDULE = @ScheduleID AND ZONE = @Zone AND PRODUCT = @ProductId
	IF @@ROWCOUNT = 0
	BEGIN
	  SET @FinalValue = @Weight
	  INSERT INTO WHS_STOCKTAKING 
	  (EVENT, ORGANIZATION, DELETED_FL, CREATE_DT, CREATEUSER, CREATECHANNEL, CREATEBRANCH,    
       CREATESCREEN, STORE, COUNTING_DT, PRODUCT, COUNTINGVALUE_AMT, ZONE, STOCKTAKINGSCHEDULE)
	  VALUES
	  (1, 1, 'N', GETDATE(), dbo.SYS_GETCURRENTUSER_FN(), dbo.SYS_GETCURRENTCHANNEL_FN(),    
       dbo.SYS_GETCURRENTBRANCH_FN(), dbo.SYS_GETCURRENTSCREEN_FN(), @StoreId, @CountingDate,    
       @ProductId, @Weight, @Zone, @ScheduleID);
	END
  END
END